5 REM DESC: Classic Tic-Tac-Toe game against the computer
10 DIM A(9) AS UBYTE: REM BOARD
15 DIM M(10) AS UBYTE: REM TO HOLD KNOWLEDGE BASE  
20 DIM W(24) AS UBYTE: REM WIN/BLOCK DATA
25 DIM D(5) AS UBYTE: REM TO HOLD MOVES IN CURRENT GAME
35 DIM FLAG AS INTEGER
40 DIM COUNT AS UBYTE
45 DIM J AS UBYTE
47 DIM H AS UBYTE
48 DIM MOVE AS UBYTE
49 DIM P AS UBYTE
50 DIM X AS UBYTE
51 DIM TEMP AS UBYTE
52 DIM B AS UBYTE
55 GOSUB 1180: REM INITIALIZE
60 REM *** PREGAME SETTINGS ***
70 FOR J=1 TO 9
80 A(J)=32
90 NEXT J
100 FOR J=1 TO 5
110 D(J)=0
120 NEXT J
130 COUNT=0
140 R$=""
150 GOSUB 1070: REM PRINT BOARD
160 REM *** MAIN CYCLE ***
170 GOSUB 460: REM MACHINE MOVE
180 GOSUB 1070: REM PRINT BOARD
190 GOSUB 870: REM WIN CHECK
200 IF R$<>"" THEN GOSUB 220
210 GOSUB 980: REM ACCEPT HUMAN MOVE
215 GOSUB 1070: REM PRINT BOARD
216 GOSUB 870: REM WIN CHECK
217 IF R$="" THEN GOTO 170
218 REM *** END MAIN CYCLE ***
219 REM ***********
220 REM END OF GAME
225 GOSUB 1070: REM PRINT BOARD
230 PRINT:PRINT
235 IF R$="W" THEN PRINT TAB(8); "I WIN": FLAG=-1
240 IF R$="L" THEN PRINT TAB(8); "YOU WIN": FLAG=1
245 IF R$="D" THEN PRINT TAB(6); "IT'S A DRAW": GOTO 380
250 REM UPDATE KNOWLEDGE BASE
270 FOR B=1 TO 5
280 FOR J=2 TO 9
290 IF M(J)=D(B) THEN GOSUB 320
300 NEXT J
310 NEXT B
315 GOTO 380
320 REM ** RE-ORDER ELEMENTS OF 'M' ARRAY **
330 REM SWAP VALUES
340 TEMP=M(J+FLAG)
350 M(J+FLAG)=M(J)
360 M(J)=TEMP
370 J = 9
375 RETURN
380 PRINT:PRINT
385 PRINT "THIS IS MY UPDATED PRIORITY"
387 PRINT:PRINT
388 FOR J=1 TO 10
389 PRINT M(J); " ";
390 NEXT J
400 PRINT:PRINT
410 REM GET INPUT
420 PRINT "PRESS ANY KEY TO CONTINUE"
430 PAUSE 0
440 GOTO 60
450 REM *************
460 REM MACHINE MOVE
470 P=79: REM ASC("O")
480 X=0
490 J=1
570 REM FIND MOVE POSITION
580 IF A(W(J))=A(W(J+1)) AND A(W(J+2))=32 AND A(W(J))=P THEN X=W(J+2): GOTO 750
590 IF A(W(J))=A(W(J+2)) AND A(W(J+1))=32 AND A(W(J))=P THEN X=W(J+1): GOTO 750
600 IF A(W(J+1))=A(W(J+2)) AND A(W(J))=32 AND A(W(J+1))=P THEN X=W(J): GOTO 750
610 IF J<21 THEN J=J+3: GOTO 580
620 IF P=79 THEN P=88: GOTO 570
630 REM ** IF NO WIN/BLOCK MOVE FOUND **
640 REM ** THEN THIS NEXT SECTION USED **
650 J=1
660 IF A(M(J))=32 THEN X=M(J): GOTO 750
670 IF J<10 THEN J=J+1: GOTO 660
680 H=0
690 H=H+1
700 X=INT(RND*9+1): IF A(X)=32 THEN GOTO 750
710 IF H<100 THEN GOTO 690
720 R$="D": REM IT IS A DRAW
730 RETURN
740 REM *****Â»***
750 REM MAKE MOVE
760 A(X)=79
770 COUNT=COUNT+1
780 D(COUNT)=X
790 FLAG=0
800 FOR J=1 TO 9
810 IF A(J)=32 THEN FLAG=1
820 NEXT J
830 IF FLAG=0 AND R$="" THEN R$="D"
840 REM IF ALL POSITIONS FULL AND R$ NOT ASSIGNED, IT IS A DRAW
850 RETURN
860 REM *********
870 REM WIN CHECK
880 J=1
890 IF A(W(J))=32 THEN J=J+3
900 IF J>23 THEN RETURN
910 IF A(W(J))=A(W(J+1)) AND A(W(J))=A(W(J+2)) THEN GOTO 940
920 IF J<22 THEN J=J+3: GOTO 890
930 RETURN
940 IF A(W(J))=79 THEN R$="W": REM MACHINE WINS
950 IF A(W(J))=88 THEN R$="L": REM MACHINE LOSES
960 RETURN
970 REM **********
980 REM HUMAN MOVE
990 PRINT:PRINT
1000 PRINT "ENTER YOUR MOVE (1-9)"
1010 LET MOVE=0
1015 IF INKEY$="" THEN GOTO 1015
1020 LET MOVE=VAL(INKEY$)
1025 IF MOVE<1 OR MOVE>9 THEN GOTO 1010
1030 IF A(MOVE)<>32 THEN GOTO 1010
1040 A(MOVE)=88
1050 RETURN
1060 REM ***********
1070 REM PRINT BOARD
1080 PRINT "{CLR}"
1090 PRINT:PRINT:PRINT
1100 PRINT"{GY 3}1 {WHT}: {GY 3}2 {WHT}: {GY 3}3 "; CHR$(A(1)); " {WHT}: {GY 3}"; CHR$(A(2)); " {WHT}: {GY 3}"; CHR$(A(3))
1110 PRINT"{WHT}---------- ----------"
1120 PRINT"{GY 3}4 {WHT}: {GY 3}5 {WHT}: {GY 3}6 "; CHR$(A(4)); " {WHT}: {GY 3}"; CHR$(A(5)); " {WHT}: {GY 3}"; CHR$(A(6))
1130 PRINT"{WHT}---------- ----------"
1140 PRINT"{GY 3}7 {WHT}: {GY 3}8 {WHT}: {GY 3}9 "; CHR$(A(7)); " {WHT}: {GY 3}"; CHR$(A(8)); " {WHT}: {GY 3}"; CHR$(A(9))
1150 PRINT
1160 RETURN
1170 REM **************
1180 REM INITIALIZATION
1190 PRINT "{CLR}"
1200 REM WIN/BLOCK DATA
1210 FOR J=1 TO 24
1220 READ W(J)
1230 NEXT J
1240 DATA 1,2,3,4,5,6,7,8,9
1250 DATA 1,4,7,2,5,8,3,6,9
1260 DATA 1,5,9,3,5,7
1270 REM INITIAL KNOWLEDGE BASE
1280 FOR J=1 TO 10
1290 READ M(J)
1300 NEXT J
1310 DATA 2,6,8,4,7,3,1,9,5,2
1320 RETURN
4500 REM RANDOM HUMAN MOVE
4510 H=0
4520 H=H+1
4530 MOVE=INT(RND*9+1)
4540 IF A(MOVE)=32 THEN A(MOVE)=88: RETURN
4550 IF H<100 THEN GOTO 4520
4560 R$="D"
4570 RETURN